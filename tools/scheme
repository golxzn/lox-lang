program       -> declaration* EOF;

declaration   -> function_decl
               | storage_decl
               | statement;

function_decl -> "fun" IDENTIFIER "(" parameters? ")" block ;

storage_decl  -> constant_decl
               | variable_decl;

constant_decl -> "const" object_decl ";" ;
variable_decl -> "var" object_decl ";" ;

object_decl   -> IDENTIFIER (":" TYPE)* "{" expression "}"
               | IDENTIFIER ":" TYPE "{" expression* "}" ;

statement     -> branch_stmt
               | print_stmt // DEBUG BUILD ONLY
               | loop_stmt
               | for_loop_stmt
               | scope_stmt
               | expression;

branch_stmt   -> "if" "(" ( declaration )* expression ")" scope_stmt
                ( "else" scope_stmt );
print_stmt    -> "print" expression ";";
loop_stmt     -> "while" "(" ( declaration )* expression ")" scope_stmt;
for_loop_stmt -> "for" "(" ( declaration | expression | ";" ) expression? ";" expression? ")" scope_stmt;
scope_stmt    -> "{" declaration* "}";

expression    -> assignment;

assignment    -> ( "++" | "--" ) IDENTIFIER
               | IDENTIFIER ( "=" | "-=" | "+=" | "*=" | "/=" ) assignment
               | logical_or;
logical_or    -> logical_and ( "or" logical_and )*;
logical_and   -> equality ( "and" equality )*;
equality      -> comparison ( ( "!=" | "==" ) comparison )*;
comparison    -> term ( ( "<=" | ">=" | "<" | ">" ) term )*;
term          -> factor ( ( "+" | "-" ) factor )*;
factor        -> unary ( ( "*" | "/" ) unary )*;

unary         -> ( "!" | "-" | "+" ) unary | primary;
call          -> primary ( arguments? )* ;
primary       -> "true" | "false" | "null"
               | NUMBER | STRING | IDENTIFIER
               | "(" expression ")";

arguments     -> expression ( "," expression )* ;
parameters    -> ( "var" | "const" ) object_decl ( "," ( "var" | "const" ) object_decl )* ;

NUMBER        -> DIGIT+ ( "." DIGIT+ )? ;
STRING        -> "\"" <any char except "\"">* "\"" ;
IDENTIFIER    -> ALPHA ( ALPHA | DIGIT )* ;
ALPHA         -> "a" ... "z" | "A" ... "Z" | "_" ;
DIGIT         -> "0" ... "9" ;
